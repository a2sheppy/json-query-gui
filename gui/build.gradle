group 'com.raybritton.jsonquery'
version '0.4.0'

buildscript {
    ext.kotlin_version = '1.1.4-2'

    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'kotlin'
apply plugin: 'application'

mainClassName = 'com.raybritton.jsonquery.GuiMainKt'
ext.versionName = version

defaultTasks 'run'

repositories {
    jcenter()
}

task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'JsonQuery GUI',
                'Implementation-Version': versionName,
                'Main-Class': mainClassName
    }
    baseName = project.name
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}


configurations {
    ktlint
}

task ktlint(type: JavaExec) {
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
    // prepend "--reporter=plain?group_by_file" arg to change the reporter
}

check.dependsOn ktlint

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    compile 'com.raybritton.jsonquery:lib:0.4.0'
    ktlint 'com.github.shyiko:ktlint:0.9.0'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}